apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply from: "../ktlint.gradle"

apply plugin: 'com.google.dagger.hilt.android'

android {
    namespace "elieomatuku.cineast_android"
    compileSdk rootProject.ext.compileSdk
    defaultConfig {
        applicationId "elieomatuku.cineast_android"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdk
        versionCode = rootProject.ext.versionCode
        versionName = rootProject.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        def apiKeyPropertiesFile = rootProject.file("apikeys.properties.txt")
        if (apiKeyPropertiesFile.exists()) {
            def apiKeyProperties = new Properties()
            apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
            buildConfigField("String", "api_key", apiKeyProperties['api_key'])
        } else {
            buildConfigField("String", "api_key", "\"${System.env.API_KEY}\"")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            resValue "string", "app_name", (rootProject.ext.appName + "_dev")
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_dev_leaks")
            buildConfigField "String", "TMDB_BASE_URL", "\"http://api.themoviedb.org/3/\""
        }

        beta {
            applicationIdSuffix = ".beta"
            resValue "string", "app_name", (rootProject.ext.appName + "_beta")
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_beta_leaks")
            buildConfigField "String", "TMDB_BASE_URL", "\"http://api.themoviedb.org/3/\""
        }

        prod {
            resValue "string", "app_name", rootProject.ext.appName
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_leaks")
            buildConfigField "String", "TMDB_BASE_URL", "\"http://api.themoviedb.org/3/\""
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://oss.sonatype.org/discoverContent/repositories/snapshots" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Local modules
    implementation project(':cache')
    implementation project(':domain')
    implementation project(':data')
    implementation project(':remote')
    implementation project(':presentation')

    implementation 'androidx.multidex:multidex:2.0.1'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //dependency
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"

    //dev utils
    implementation 'com.github.ajalt:timberkt:1.4.0'
    implementation "com.jakewharton.timber:timber:4.7.1"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
